extends Node
class_name FiniteStateMachine

var states : Dictionary = {}
var current_state : State
@export var initial_state : State
@export var player : Player

#NOTE This is a generic finite_state_machine, it handles all states, changes to this code will affect
	# everything that uses a state machine!

func _ready():
	for child in get_children():
		if child is State:
			states[child.name.to_lower()] = child
			child.state_transition.connect(change_state)

	if initial_state:
		initial_state.Enter()
		current_state = initial_state

#Call the current states update function continuosly
func _process(delta):
	if !get_parent().is_local_player:
		return
	if current_state:
		current_state.Update(delta)

func _physics_process(delta: float) -> void:
	if !get_parent().is_local_player:
		return
	if current_state:
		current_state.Process_update(delta)
#region State Management
#Use force_change_state cautiously, it immediately switches to a state regardless of any transitions.
#This is used to force us into a 'death state' when killed
func force_change_state(new_state : String):
	if !get_parent().is_local_player:
		return
	var newState = states.get(new_state.to_lower())
	
	if !newState:
		print(new_state + " does not exist in the dictionary of states")
		return
	
	if current_state == newState:
		print("State is same, aborting")
		return
		
	if current_state:
		var exit_callable = Callable(current_state, "Exit")
		exit_callable.call_deferred()
	
	newState.Enter()
	
	current_state = newState

	# Atualiza o player.current_state com o nome do estado atual
	if player:
		player.current_state = current_state.name.to_lower()


func change_state(source_state : State, new_state_name : String):
	if !get_parent().is_local_player:
		return
	if source_state != current_state:
		# print("Invalid change_state trying from: " + source_state.name + " but currently in: " + current_state.name)
		return

	var new_state = states.get(new_state_name.to_lower())
	if !new_state:
		print("New state is empty")
		return
		
	if current_state:
		current_state.Exit()
		
	new_state.Enter()
	
	current_state = new_state

	# Atualiza o player.current_state com o nome do estado atual
	if player:
		player.current_state = current_state.name.to_lower()


#endregion
